import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

plugins {
	id 'java'
	id 'maven'
	id 'maven-publish'
	id 'idea'
	id 'eclipse'
	id 'signing'
	id 'checkstyle'
	id 'jacoco'
	id "com.diffplug.gradle.spotless" version "3.27.0"
	id 'org.sonarqube' version '2.8'
	// id "io.codearte.nexus-staging" version "0.12.0"
	id 'com.github.johnrengelman.shadow' version '5.0.0'
}

apply from: 'dependencies.gradle'
apply from: 'gradle/quality.gradle'
apply from: 'gradle/deployment.gradle'
apply from: 'gradle/execute.gradle'

description = "Atlas Checks"

sourceCompatibility = 11
targetCompatibility = 11

repositories
{
    // For geotools
    maven { url "http://download.osgeo.org/webdav/geotools/" }
    mavenCentral()
    // For Spark CDH
    maven { url "https://repository.cloudera.com/content/repositories/releases/" }
    // For jetty (through spark)
    maven { url "http://repository.cloudera.com/cloudera/cloudera-repos/" }
    // For staged repositories (before they get to maven central)
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

configurations
{
    all {
        resolutionStrategy {
            // Force the version compatible with the latest version
            // of Spark brought in by atlas generator
            force 'com.fasterxml.jackson.core:jackson-core:2.6.7'
            force 'com.fasterxml.jackson.core:jackson-databind:2.6.7'
        }
    }
    compile
    {
        resolutionStrategy
        {
            force packages.atlas
            force packages.atlas_generator

            // Snappy 1.1.1.6 is the one that has the proper .so libs.
            // https://github.com/xerial/snappy-java/issues/6
            force 'org.xerial.snappy:snappy-java:1.1.1.6'
        }
    }
    shaded
    {
        resolutionStrategy
        {
            force packages.atlas
            force packages.atlas_generator

            // Snappy 1.1.1.6 is the one that has the proper .so libs.
            // https://github.com/xerial/snappy-java/issues/6
            force 'org.xerial.snappy:snappy-java:1.1.1.6'
        }
        // Hadoop and Spark are way too fat.
        exclude group: 'org.apache.hadoop'
        exclude group: 'org.apache.spark'
        exclude group: 'org.scala-lang'
        exclude group: 'org.slf4j'
    }
    runtime
    archives
}

dependencies
{
    compile packages.commons
    compile packages.atlas
    compile packages.atlas_generator
    compile packages.postgis
    compile packages.postgres
    compile packages.spring
    compile packages.mockito

    checkstyle packages.checkstyle
    checkstyle packages.atlas_checkstyle

    shaded project.configurations.getByName('compile')
}

/**
 * Artifact related items
 */
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task shaded(type: Jar) {
    baseName = project.name
    classifier = 'shaded'
    from {
        configurations.shaded.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                exclude {
                    it.path.contains('META-INF') && (it.path.endsWith('.SF') || it.path.endsWith('.DSA') || it.path.endsWith('.RSA'))
                }
            }
        }
    }
    with jar
    zip64 = true
}

shadowJar {
    baseName = project.name
    mergeServiceFiles()
    transform(com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer) {
        resource = 'reference.conf'
    }
    zip64 = true
}

artifacts
{
    archives javadocJar
    archives sourcesJar
    archives shaded
}

/*
 * This is to skip the tasks for which there is a skip<TaskName>=true
 * environment variable
 */
def skippedTaskNames = System.getenv().findAll { key, value ->
    key.startsWith("skip") && value.equalsIgnoreCase("true")
}.keySet().collect { it.substring(4) }
gradle.startParameter.excludedTaskNames += skippedTaskNames

idea {
    project {
        languageLevel = '11'
    }
}
